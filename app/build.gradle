plugins {
    alias(libs.plugins.android.application)
    id 'androidx.navigation.safeargs'
    id 'com.gladed.androidgitversion' version '0.4.14'
}

androidGitVersion {
    format '%tag%%.devcount%%+commit%'
}

android {
    namespace 'de.vdvcount.app'
    compileSdk 34

    defaultConfig {
        applicationId "de.vdvcount.app"
        minSdk 24
        targetSdk 34

        versionCode androidGitVersion.code()
        versionName androidGitVersion.name()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def props = new Properties()
            def localPropsFile = rootProject.file("local.properties")
            if (localPropsFile.exists()) {
                props.load(new FileInputStream(localPropsFile))
            }

            storeFile file(props.getProperty("RELEASE_STORE_FILE"))
            storePassword props.getProperty("RELEASE_STORE_PASSWORD")
            keyAlias props.getProperty("RELEASE_KEY_ALIAS")
            keyPassword props.getProperty("RELEASE_KEY_PASSWORD")
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.all { output ->
                def versionName = variant.versionName
                def baseName = "app-${variant.name}-${versionName}.apk"
                outputFileName = baseName
            }
        }
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        debug {

        }
        release {
            signingConfig signingConfigs.release

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    dataBinding {
        enabled = true
    }
}

dependencies {

    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
    implementation libs.lifecycle.livedata.ktx
    implementation libs.lifecycle.viewmodel.ktx

    implementation libs.navigation.fragment.ktx
    implementation libs.navigation.ui.ktx

    implementation 'androidx.camera:camera-camera2:1.2.0-alpha04'
    implementation 'androidx.camera:camera-lifecycle:1.2.0-alpha04'
    implementation 'androidx.camera:camera-view:1.2.0-alpha04'

    implementation 'com.google.mlkit:barcode-scanning:17.1.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'

    implementation libs.android.permissions
    implementation libs.retrofit
    implementation libs.converter.gson
    implementation libs.legacy.support.v4

    implementation libs.timelineview

    testImplementation libs.junit

    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}